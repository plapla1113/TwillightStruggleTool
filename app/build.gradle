apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.0"
    defaultConfig {
        applicationId "tw.chilinandwilson.twillightstruggletool"
        minSdkVersion 18
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled = true
    }
    buildTypes {
        debug {
            testCoverageEnabled = true
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // Share code and resources between test and androidTest: https://stackoverflow.com/a/37847307
    sourceSets {
        String sharedTestJavaDir = 'src/sharedTest/java'
        String sharedTestResourcesDir = 'src/sharedTest/resources'

        test {
            java.srcDirs += [sharedTestJavaDir]
            resources.srcDirs += [sharedTestResourcesDir]
        }

        androidTest {
            java.srcDirs += [sharedTestJavaDir]
            resources.srcDirs += [sharedTestResourcesDir]
        }
    }
}

ext.dbflowVersion = "4.0.5"
ext.dexmakerVersion = "2.2.0"
ext.mockitoVersion = "1.10.19"
ext.daggerVersion = "2.9"

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:26.+'
    compile 'com.android.support:design:26.+'
    compile 'com.android.support:support-vector-drawable:26.+'
    compile 'com.android.support:support-v4:26.+'
    compile 'com.android.support:cardview-v7:26.+'

    compile 'com.google.code.gson:gson:2.8.0'

    // https://github.com/Raizlabs/DBFlow
    annotationProcessor "com.github.Raizlabs.DBFlow:dbflow-processor:$dbflowVersion"
    compile "com.github.Raizlabs.DBFlow:dbflow-core:$dbflowVersion"
    compile "com.github.Raizlabs.DBFlow:dbflow:$dbflowVersion"
    compile "com.github.Raizlabs.DBFlow:dbflow-rx2:$dbflowVersion"

    // https://github.com/ReactiveX/RxJava
    compile 'io.reactivex.rxjava2:rxjava:2.0.8'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'

    // https://github.com/google/guava
    // https://android.benigumo.com/20170123/guava-21-0-errorerror-converting-bytecode-to-dex-cause-dex-cannot-parse-version-52-byte-code/
    compile 'com.google.guava:guava:20.0'

    // https://developer.android.com/studio/build/multidex.html
    compile 'com.android.support:multidex:1.0.1'

    // Dagger2: https://github.com/codepath/android_guides/wiki/Dependency-Injection-with-Dagger-2
    compile "com.google.dagger:dagger:$daggerVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"
    provided 'javax.annotation:jsr250-api:1.0'
    // Dagger2: Add for androidTest to generate dagger component
    androidTestAnnotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"

    testCompile 'junit:junit:4.12'
    testCompile "org.mockito:mockito-core:$mockitoVersion"
    // Mockito is also needed in androidTest
    androidTestCompile "org.mockito:mockito-core:$mockitoVersion"
    // dexmaker: https://github.com/linkedin/dexmaker
    androidTestCompile "com.linkedin.dexmaker:dexmaker-mockito:$dexmakerVersion"
}
